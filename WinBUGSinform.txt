model {

pi <- 3.141592
piover2 <- pi/2

for(i in 1:N) {
	y[i] ~ dbern(p[i])
	for(e in 1:NEnv){
		x.clim.C[i,e] <- x.clim[i,e]-ax[e]
		x.clim.beta[i,e,1] <- -beta[e,1]*x.clim.C[i,e]
		x.clim.beta[i,e,2] <- beta[e,which.beta[e]]*x.clim.C[i,e]
		x.clim.Calc2[i,e] <- max(x.clim.beta[i,e,1],x.clim.beta[i,e,2])
		x.clim.Calc[i,e] <- x.clim.Calc2[i,e]*abs(x.clim.C[i,e])
	}
	for(e1 in 1:(NEnv-1)){
		for(e2 in 1:e1){
			x.clim.CalcCross[i,e1,e2] <- 0
		}
		for(e2 in (e1+1):NEnv){
			x.clim.CalcCross[i,e1,e2] <- gamma[e1,e2]*x.clim.C[i,e1]*x.clim.C[i,e2]
		}
	}
	for(e in 1:NEnv){
		x.clim.CalcCrossColSum[i,e] <- sum(x.clim.CalcCross[i,,e])
	}
	x.clim.CalcSum[i] <- sum(x.clim.Calc[i,1:NEnv])
	x.clim.CalcCrossSum[i] <- sum(x.clim.CalcCrossColSum[i,])
	x.clim.Total.tmp[i] <- az - sqrt(x.clim.CalcSum[i] + x.clim.CalcCrossSum[i])
	x.clim.Total[i] <- min(x.clim.Total.tmp[i], beta0)
	logit(p[i]) <- x.clim.Total[i] + u[u.clique.start[clique[i]]+clique.i[i]-1]
}
beta0.prec <- 1/beta0.var
beta0.diff ~ dnorm(beta0.mu,beta0.prec)
az ~ dnorm(0,0.1)
beta0 <- az - exp(beta0.diff)
for(i in 1:NonSingletonClique){
	clique.length[nonsingleton.clique.list[i]] <- u.clique.end[nonsingleton.clique.list[i]]-u.clique.start[nonsingleton.clique.list[i]]+1
	u[u.clique.start[nonsingleton.clique.list[i]]:u.clique.end[nonsingleton.clique.list[i]]] ~ car.normal(adj[adj.clique.start[nonsingleton.clique.list[i]]:adj.clique.end[nonsingleton.clique.list[i]]], weights[1:clique.length[nonsingleton.clique.list[i]]], num[u.clique.start[nonsingleton.clique.list[i]]:u.clique.end[nonsingleton.clique.list[i]]], tau)
}
for(k in 1:adj.clique.end[NClique]) {
	weights[k] <- 1
}
tau <- car.tau

for(e in 1:1){
	ax.prec[e] <- 1/ax.var[e]
	ax[e] ~ dnorm(ax.mu[e],ax.prec[e])I(-1,2)
	beta.prec[e,1] <- 1/beta.var[e,1]
	beta.prec[e,2] <- 1/beta.var[e,2]
	beta[e,1] ~ dnorm(beta.mu[e,1],beta.prec[e,1])I(0,)
	beta[e,2] ~ dnorm(beta.mu[e,2],beta.prec[e,2])I(0,)
}

yp <- log(0.02/0.98)
for(e in 2:2){
	ax.prec[e] <- 1/ax.var[e]
	ax[e] ~ dnorm(ax.mu[e],ax.prec[e])I(-1,2)
	beta.prec[e,1] <- 1/beta.var[e,1]
	beta.prec[e,2] <- 1/beta.var[e,2]
	beta[e,1] ~ dnorm(beta.mu[e,1],beta.prec[e,1])I(0,)
	beta[e,2] <- ((az-yp)/(1.032821-ax[e]))*((az-yp)/(1.032821-ax[e]))
}

for(e1 in 1:(NEnv-1)){
	gamma.temp[e1,1] <- 0
	for(e2 in (e1+1):NEnv){
		gamma.temp[e1,e2] ~ dunif(0,1)
		min.gamma.1[e1,e2] <- beta[e1,1]*beta[e2,1]
		min.gamma.2[e1,e2] <- beta[e1,which.beta[e1]]*beta[e2,which.beta[e2]]
		max.gamma.1[e1,e2] <- beta[e1,1]*beta[e2,which.beta[e2]]
		max.gamma.2[e1,e2] <- beta[e1,which.beta[e1]]*beta[e2,1]
		min.gamma[e1,e2] <- -(2/(NEnv-1))*sqrt(min(min.gamma.1[e1,e2],min.gamma.2[e1,e2]))
		max.gamma[e1,e2] <- (2/(NEnv-1))*sqrt(min(max.gamma.1[e1,e2],max.gamma.2[e1,e2]))
		gamma[e1,e2] <- min.gamma[e1,e2]+gamma.temp[e1,e2]*(max.gamma[e1,e2]-min.gamma[e1,e2])
	}
}
for(e2 in 1:NEnv){
	gamma.temp[NEnv,e2] <- 0
}

}
